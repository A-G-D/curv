let
Type = 5; // symmetry group type, range 3..5
U = 0; // barycentric coordinate for the principle node, range 0..1
V = 0; // range 0..1
W = 1; // range 0..1
SRadius = 0.1; // segments radius

nc = [-0.5,-cospin,scospin];
cospin = cos(pi/Type);
scospin = sqrt(0.75-cospin*cospin);

p = normalize(U*pab+V*pbc+W*pca);
pab = [0,0,1];
pbc = normalize[scospin,0,0.5];
pca = normalize[0,scospin,cospin];

fold pt =
    do  local pos = pt;
        for (i in 0..Type) (
            pos := [abs(pos.[X]), abs(pos.[Y]), pos.[Z]];
            let t = -2*min[0,dot[pos,nc]];
            in pos := pos + t*nc;
        );
    in pos;

D2Segments pt =
    let pos = pt - p;
        dla = mag(pos-min(0,pos.[X])*[1,0,0]);
        dlb = mag(pos-min(0,pos.[Y])*[0,1,0]);
        dlc = mag(pos-min(0,dot[pos,nc])*nc);
    in min[min[dla,dlb],dlc]-SRadius;//max[max[dla,dlb],max[dlc,dlp]]-SRadius;

in
make_shape {
    dist pt = D2Segments(fold(pt.[[X,Y,Z]]));
    is_3d = true;
    bbox = [[-1,-1,-1]-SRadius, [1,1,1]+SRadius];
}
