// Schwartz "P" surface:
// https://en.wikipedia.org/wiki/Schwarz_minimal_surface#Schwarz_P_.28.22Primitive.22.29
schwartz_p = make_shape {
    dist p: -(cos(p[X]) + cos(p[Y]) + cos(p[Z])),
    is_3d: true
};

schwartz_bones = schwartz_p >> isosurface (-1) >> df_scale 0.66;
schwartz_bones2 = complement schwartz_p >> isosurface (-1) >> df_scale 0.66;
double_schwartz_bones = union(
    colour blue schwartz_bones,
    colour green schwartz_bones2,
);

schwartz_surface = shell (.1) schwartz_p >> df_scale 0.66;

schwartzize t shape = morph(t, shape, intersection(schwartz_surface, shape));

union(
    schwartz_surface >> crop (sphere(tau*3/2)) >> translate(-15,0,0),
    intersection(cube (tau*3), schwartz_surface >> colour red) >> translate(+15,0,0),
    schwartzize(.1) (cube 20) >> translate(+45,0,0),
)

//schwartz_p >> df_scale 0.5 >> crop (cube 20)

//schwartz_p >> translate(0,0,1) >> slice_xy >> gradient 2
