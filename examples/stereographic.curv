// Stereographic Projection
// Mapping an infinite plane onto the surface of a unit sphere.
// https://en.wikipedia.org/wiki/Stereographic_projection

stereographic_projection plane = sphere 1 >> colour
    (p->plane.colour(p[X]/(1-p[Z]), p[Y]/(1-p[Z]), 0));

log_spiral(a,b) = make_shape {
    dist p : (
        var r := mag p;
        var t := phase p;
        if (r == 0)
            0
        else (
            var n := (log(r/a)/b - t) / tau;
            var upper_r := a * e^(b*(t+tau*ceil n));
            var lower_r := a * e^(b*(t+tau*floor n));
            min(abs(upper_r-r), abs(r-lower_r)) - r*b
        )
    ),
    bbox : everything.bbox,
    is_2d : true,
};
spiral = union(colour red everything, log_spiral(1,.2));

spiral >> stereographic_projection
