// Lipschitz constant == 4/3. Apply 'df_scale 0.66' after extracting isosurfaces
gyroid = make_shape {
    dist p : cos(p[X])*sin(p[Y]) + cos(p[Y])*sin(p[Z]) + cos(p[Z])*sin(p[X]),
    bbox: everything.bbox,
    is_3d: true
};

gyroid_bones = isosurface (-1.2) gyroid >> df_scale 0.66;

gyroid_surface = shell (.1) gyroid >> df_scale 0.66;

gyroidize t shape = morph(t, shape, intersection(gyroid_surface, shape));

c = 1.5;
double_gyroid_bones = union(
    colour blue gyroid_bones,
    colour green gyroid_bones >> translate (c,c,c),
);

union(
    smooth_intersection .5 (sphere 10, double_gyroid_bones) >> translate(-20,0),
    intersection (sphere 10, gyroid_surface),
    gyroidize(.1) (cube 18) >> translate(+20,0,0),
)
