hsv2rgb(c) = (
    var m := abs(mod(c'0 + [1,2/3,1/3], 1) * 6 - 3);
    c'2 * mix(1, clamp3(m - 1, 0, 1), c'1)
);
mix(a,b,t) = a*(1-t) + b*t;
clamp3(v,lo,hi) = [
    min(max(v'X,lo),hi),
    min(max(v'Y,lo),hi),
    min(max(v'Z,lo),hi),
];
dot p = p'X*p'X + p'Y*p'Y;
shape2d {
  dist : everything.dist,
  bbox : everything.bbox,
  colour pt : (
    var p := pt[X,Y];
    var color := [0,0,0];
    var i := 0;
    while (i < 100) (
      p := [p'X*p'X - p'Y*p'Y, 2*p'X*p'Y]+pt[X,Y];
      if (dot p > 4) (
        var colorRegulator := (i-1)-log(log(dot p)/log 2)/log 2;
        color := [0.95 + .012*colorRegulator, 1, .2+.4*(1+sin(.3*colorRegulator))];
        i := 100;
      ) else i := i + 1;
    );
    hsv2rgb color
  ),
  is_2d : true,
  is_3d : false,
}
