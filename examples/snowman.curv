parametric
  smoothness :: slider[0.01, 5] = 1.75;
in
let
  include lib.builder;

  head = ctx ->
   '{' ctx
    >> '*' (sphere 10) [0, 0, 0]
      >> '{'
        >> '><' union
        >> '*' (sphere 1) [-1, -2, 5 - 0.5]
        >> '*' (sphere 1) [1, -2, 5 - 0.5]
        >> '><' ((smooth smoothness).union)
        >> '*' (cone { d: 1, h: 4 }) [0, 0, 5 - 0.5]
      >> '}'
    >> '}';

  torso = ctx ->
    '{' ctx
    >> '*' (sphere 20) [0, 12, 0]
      >> '{'
        >> '><' union
        >> '*' (sphere 1) [0, 0, 10 - 0.2]
        >> '*' (sphere 1) [0, -2, 10 - 0.4]
        >> '*' (sphere 1) [0, -4, 10 - 1.0]
      >> '}'
      >> '{'
        >> '><' ((smooth smoothness).union)
        >> '*' (
          cone { d: 3, h: 10 }
          >> rotate { angle: (-90*deg), axis: Y_axis }
        ) [-10, 0, 0]
        >> '*' (
          cone { d: 3, h: 10 }
          >> rotate { angle: (90*deg), axis: Y_axis }
        ) [10, 0, 0]
      >> '}'
    >> '}';

  bottom = ctx ->
    '{' ctx
      >> '*' (sphere 30) [0, 20, 0]
    >> '}';

  snowman =
    build
      >> '><' ((smooth smoothness).union)
      >> head
      >> '><' ((smooth smoothness).union)
      >> torso
      >> '><' ((smooth smoothness).union)
      >> bottom
    >> done;
in
snowman

